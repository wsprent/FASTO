fun int readInt(int i) = 
    let x = write("Introduce (Next) Number ") in 
    let a = write(i) in let x = write(": ")   in
    read(int)

fun [int] readIntArr(int n) = 
    let itsp = iota(n)
    in  map(readInt, itsp)

fun int writeInt(int i) = write(i)

fun int writeIntArr([int] arr) = 
    let a = write(" { ") in let v = map(writeInt, arr) in let a = write(" }\n") in 1

fun int max(int x, int y) = if(x<y) then y else x
// >

fun [int] mapfun(int x) = 
    let xm = max(x, 0) 
    in  {xm, xm, xm, x}

fun [int] redfun([int] a, [int] b) = 
    let mss = max( max(a[0], b[0]), a[2] + b[1] ) in
    let mis = max( a[1], a[3] + b[1] ) in
    let mcs = max( a[2] + b[3], b[2] ) in
    let ts  = a[3] + b[3]
    in {mss, mis, mcs, ts}


fun [int] mssp(int n) = 
    let inarr  = readIntArr(n) in
//    let inarr  = {1, 0-2, 3, 4, 0-1, 5, 0-6, 1} in 
    let maparr = map(mapfun, inarr) in
    let ne     = {0, 0, 0, 0}
    in  reduce(redfun, ne, maparr) 

fun int main() = 
    let arr = mssp(8) in 
        let x = write("\n\nMSSP Result Is: ") in
            let p   = writeInt(arr[0]) in 
                let x = write("\n") in p

